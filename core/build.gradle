plugins {
    id "java"
    id "maven"
    id "maven-publish"
}

group 'com.epam.cme'
version '2.1.4'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

jar {
    baseName 'b2bits-jmdp3-core'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    baseName 'b2bits-jmdp3-core'
}

artifacts {
    archives sourcesJar    
}

task copyToLib(type: Copy) {
    into "$buildDir/libs"
    from configurations.runtime
}
build.dependsOn(copyToLib)

dependencies {
    implementation "javax.xml.bind:jaxb-api:2.3.1"
    implementation "com.sun.xml.bind:jaxb-core:2.3.0"
    implementation "com.sun.xml.bind:jaxb-impl:2.3.0"
    implementation "javax.activation:activation:1.1.1"
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

    compile group: 'net.openhft', name: 'chronicle-bytes', version: '2.27ea0'
    compile group: 'commons-configuration', name: 'commons-configuration', version: '1.10'
    runtime group: 'commons-collections', name: 'commons-collections', version: '3.2.2'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.epam.cme'
            artifactId = 'core'
            version = '2.1.4'
            from components.java

            pom {
                name = 'EPAM MDP3 MBP Only'
            }
        }
    }

    repositories {
        maven {
            url project.findProperty('mahiNexusUrl') ?: System.getenv('MAHI_NEXUS_URL')
            name = 'nexus'
            credentials {
                username = project.findProperty('nexusUsername') ?: System.getenv('NEXUS_USERNAME')
                password = project.findProperty('nexusPassword') ?: System.getenv('NEXUS_PASSWORD')
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}