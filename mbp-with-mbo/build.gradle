plugins {
    id "java"
    id "maven"
    id "java-library-distribution"
    id "me.champeau.gradle.jmh" version "0.5.0-rc-1"
    id "maven-publish"
}

group = 'com.epam.cme'
version = '2.1.5-MAHI14'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

jar {
    archiveBaseName.set('b2bits-jmdp3-mdp-with-mbo')
}

dependencies {
    compile project(':core')
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.5'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.5'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
    runtime group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.5'
    compile group: 'net.openhft', name: 'koloboke-api-jdk8', version: '0.6.8'
    runtime group: 'net.openhft', name: 'koloboke-impl-jdk8', version: '0.6.8'
    compile group: 'net.openhft', name: 'chronicle-core', version: '1.7.6'
    compile group: 'org.agrona', name: 'agrona', version: '0.9.5'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'uk.co.real-logic', name: 'sbe-tool', version: '1.6.0'
    testCompile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
}

distributions {
    main {
        baseName = 'b2bits-jmdp3'
        contents {
            from { '../dist' }
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
    archiveBaseName.set('b2bits-jmdp3-mdp-with-mbo')
}

artifacts {
    archives sourcesJar    
}

task copyToLib(type: Copy) {
    into "$buildDir/libs/lib"
    from configurations.runtime
}
build.dependsOn(copyToLib)

task testJar(type: Jar) {
    archiveClassifier.set('tests')
    from sourceSets.test.output
    archiveBaseName.set('b2bits-jmdp3-mdp-with-mbo')
    manifest {
        attributes 'Main-Class': 'com.epam.cme.mdp3.test.MBOWithMBPMain'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'b2bits-jmdp3-mdp-with-mbo'
        }
    }
    repositories {
        maven {
            name = findProperty('nexusRepoId') ?: System.getenv('NEXUS_REPO_ID')
            url = findProperty('nexusRepoUrl') ?: System.getenv('NEXUS_REPO_URL')
            credentials {
                username = findProperty('nexusUsername') ?: System.getenv('NEXUS_USERNAME')
                password = findProperty('nexusPassword') ?: System.getenv('NEXUS_PASSWORD')
            }
        }
    }
}
