plugins {
    id "java"
    id "maven"
    id "java-library-distribution"
    id "org.sonarqube" version "2.7.1"
    id "me.champeau.gradle.jmh" version "0.5.0-rc-1"
    id "maven-publish"
}
group = 'com.epam.cme'
version = '2.1.5-MAHI7'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

jar {
    archiveBaseName.set('b2bits-jmdp3-mdp-only')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
    archiveBaseName.set('b2bits-jmdp3-mdp-only')
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

dependencies {
    compile project(':core')
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.5'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.5'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
    runtime group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.5'
    compile group: 'net.openhft', name: 'koloboke-api-jdk8', version: '0.6.8'
    runtime group: 'net.openhft', name: 'koloboke-impl-jdk8', version: '0.6.8'
    compile group: 'net.openhft', name: 'chronicle-core', version: '2.27ea7'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'uk.co.real-logic', name: 'sbe-tool', version: '1.6.0'
    testImplementation 'io.cucumber:cucumber-java:4.3.1'
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'gradle.cucumber', 'src/cucumber/resources']
        }
    }
}

artifacts {
    archives sourcesJar    
}

task copyToLib(type: Copy) {
    into "$buildDir/libs/lib"
    from configurations.runtime
}
build.dependsOn(copyToLib)

distributions {
    main {
        baseName = 'b2bits-jmdp3'
        contents {
            from { '../dist' }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'b2bits-jmdp3-mdp-only'
        }
    }
    repositories {
        maven {
            name = findProperty('nexusRepoId') ?: System.getenv('NEXUS_REPO_ID')
            url = findProperty('nexusRepoUrl') ?: System.getenv('NEXUS_REPO_URL')
            credentials {
                username = findProperty('nexusUsername') ?: System.getenv('NEXUS_USERNAME')
                password = findProperty('nexusPassword') ?: System.getenv('NEXUS_PASSWORD')
            }
        }
    }
}
